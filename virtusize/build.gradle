plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
}

android {
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    buildTypes {
        debug {
            buildConfigField "String", "VERSION_NAME", "\"${VERSION_NAME}\""
            consumerProguardFiles 'proguard-rules.pro'
        }

        release {
            buildConfigField "String", "VERSION_NAME", "\"${VERSION_NAME}\""
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

configurations {
    ktlint
}

dependencies {
    api project(":virtusize-core")
    api project(":virtusize-auth")

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation 'androidx.browser:browser:1.3.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "com.google.truth:truth:1.1.2"
    testImplementation 'org.json:json:20210307'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutine_version"

    ktlint "com.pinterest:ktlint:$ktlint_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt",
            // to generate report in checkstyle format prepend following args:
            "--reporter=plain", "--reporter=html,output=${buildDir}/ktlint.html"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

ext {
    artifactId = "virtusize"
    artifactName = 'virtusize'
    artifactDescription = "Virtusize Android SDK"
}

apply from: "${rootDir}/gradle/deploy.gradle"